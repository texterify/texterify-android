plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id("org.jetbrains.dokka") version "1.4.10.2"
    id "com.jfrog.bintray"

    id 'maven-publish'
}

group = 'com.texterify.android.ota'
version = System.getenv('TRAVIS_TAG')

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = project.group
                artifactId = project.archivesBaseName
                version project.version

                artifact(tasks.getByName("generateSourcesJar"))
                artifact(tasks.getByName("generateJavadocsJar"))
            }
        }
    }
}

bintray {
    user = 'texterify-api'
    key = System.getenv('BINTRAY_KEY')

    publications = ['release']

    pkg {
        repo = 'mvn'
        name = "$project.group.$archivesBaseName"
        userOrg = 'texterify'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/texterify/texterify-android.git'
        version {
            name = project.version
            desc = ''
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG')
        }
    }
}

task generateSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task generateJavadocsJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from tasks.getByName("dokkaJavadoc").outputDirectory
}

dependencies {
    implementation project(':texterify-ota')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}